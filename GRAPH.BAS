10 ON ERROR GOTO 350
20 KEY OFF:SCREEN 0:WIDTH 80:CLS
30 PRINT "Function FN f(x) or Enter function in x (F/E): ";:FX$=CHR$(ASC(INPUT$(1)) AND 223)
40 PRINT
50 PRINT CHR$ (241); " value [(T)rue/(F)alse]: "; : POSITIVE.NEGATIVE$ = CHR$(ASC(INPUT$ (1)) AND 223)
60 IF POSITIVE.NEGATIVE$ = "T" THEN GOSUB 570 ELSE 80
70 ON ERROR GOTO 350:KEY 2, "RUN" + CHR$ (13) : FX$ = "F" : POSITIVE.NEGATIVE$ = "T" : GOTO 90
80 PRINT : IF FX$ = "E" THEN GOSUB 380 ELSE GOSUB 490
90 DEF FN F(X)=5*X^7+4*X^6+3*X^5+2*X^4+X^3+9*X^2+8*X
100 DEF FN FIXED (X) = X/2
110 DEF FN VAR (X) = SQR (48 - 3 * X ^ 2) / 2
120 DEF FN ASPECT.RATIO = 4/3
130 INPUT "Lower x value: ",X1.OLD
140 INPUT "Upper x value: ",X2.OLD
150 INPUT "Step value (Enter for default): ",STEP.VALUE:IF STEP.VALUE=0 THEN STEP.VALUE = (X2.OLD - X1.OLD)*FN ASPECT.RATIO/1280
160 INPUT "Lower y limit: ", Y1:INPUT "Upper y limit (Enter for square graph): ",Y2:IF Y2<>0 THEN 180
170 Y2 = X2.OLD - X1.OLD + Y1:PRINT "Upper y limit is ";Y2;". Press <Esc> to type in new limits: ":IF INPUT$(1) = CHR$(27) THEN 130
180 X1 = X1.OLD * (1 - SGN (X1.OLD) * (FN ASPECT.RATIO - 1)): X2 = X2.OLD * (1 + SGN(X2.OLD) * (FN ASPECT.RATIO - 1))
190 PRINT:PRINT "Resolution (H/L): ";:RES$=CHR$(ASC(INPUT$(1)) AND 223)
195 IF RES$="L" THEN INPUT "Color number (0-3): ",COLOUR ELSE COLOUR = 3
200 PRINT:INPUT "Load screen (Enter for none): ", FILE.NAME$
210 IF RES$ = "L" THEN SCREEN 1, 1:OUT &H3D9,1:ELSE SCREEN 2:OUT &H3D9,7
220 IF FILE.NAME$<>"" THEN DEF SEG=&HB800:BLOAD FILE.NAME$, 0:DEF SEG
230 WINDOW (X1, Y1)-(X2, Y2)
240 LINE (0,Y1)-(0,Y2)
250 LINE (X1,0)-(X2,0)
252 FOR Z=X1.OLD TO X2.OLD STEP (X2.OLD - X1.OLD) / 10
254 LINE (Z, (Y2-Y1)/80)-(Z, -(Y2-Y1)/80),3
256 NEXT Z:LINE (X2.OLD, (Y2-Y1)/80)-(X2.OLD, -(Y2-Y1)/80)
257 FOR Z=Y1 TO Y2 STEP (Y2 - Y1) / 10
258 LINE ((X1-X2)/128, Z)-((X2-X1)/128,Z),3
259 NEXT Z:LINE ((X1-X2)/128, Y2)-((X2-X1)/128,Y2),3
260 FOR X=X1 TO X2 STEP STEP.VALUE
270 IF FX$ = "E" THEN GOSUB 460 : GOTO 300
280 IF POSITIVE.NEGATIVE$ = "F" THEN Y=FN F(X) : GOTO 300
290 FLAG = 0 : Y = FN FIXED (X) + FN VAR (X)
300 REM Debug ' LOCATE 1, 1: PRINT USING "X: ####.####    Y: ####.####"; X, Y
310 PSET (X,Y), COLOUR
320 IF POSITIVE.NEGATIVE$ = "T" AND FLAG = 0 THEN Y=FN FIXED (X)-FN VAR (X) : FLAG = 1 : GOTO 300
330 NEXT
340 GOTO 700
350 IF ERL >= 270 AND ERL <= 290 THEN Y = 0
360 REM Debug 'LOCATE 25,1 : PRINT "Error #"ERR" in line #"ERL;
370 RESUME NEXT
380 INPUT "Number of terms in function: ",TERMS
390 DIM EXPN(TERMS,2)
400 FOR X=0 TO (TERMS-1)
410 PRINT "Term #"X+1;
420 INPUT ": Co-efficient: ",EXPN(X,0)
430 LOCATE CSRLIN-1,40:INPUT "Power: ",EXPN(X,1)
440 NEXT
450 RETURN
460 Y=0
470 FOR A=0 TO (TERMS-1)
480 Y=Y+EXPN(A,0)*X^EXPN(A,1):NEXT A:RETURN
490 PRINT "Edit the function and press F2 to continue: "
500 KEY 2, "RUN 540"+CHR$(13)
510 EDIT 90
520 KEY 2,"RUN"+CHR$(13)
530 RETURN
540 FX$="F"
550 KEY 2,"RUN"+CHR$(13):ON ERROR GOTO 350:POSITIVE.NEGATIVE$="F"
560 GOTO 90
570 PRINT : PRINT "Enter fixed-sign terms :-"
580 GOSUB 620
590 PRINT : PRINT "Enter variable-sign terms :-"
600 GOSUB 660
610 RETURN
620 PRINT "Edit the following line and press F2 :-"
630 KEY 2, "Run 590" + CHR$ (13)
640 EDIT 100
650 RETURN
660 PRINT "Edit the following line and press F2 :-"
670 KEY 2, "Run 70" + CHR$ (13)
680 EDIT 110
690 RETURN
700 A$=INPUT$(1)
710 DEF SEG=&HB800:BSAVE "GRAPH.TMP", 0, 16192
720 SCREEN 0, 0, 1: WIDTH 80
730 INPUT "Enter save file name (Enter for none): ", FILE.NAME$
740 IF RES$="H" THEN SCREEN 2 : OUT &H3D9, 7
750 IF RES$="L" THEN SCREEN 1 : CLS : COLOR 1 : OUT &H3D9, 1
760 BLOAD "GRAPH.TMP", 0
770 IF FILE.NAME$ <> "" THEN BSAVE FILE.NAME$, 0, 16192
780 KILL "GRAPH.TMP"
790 A$=INPUT$(1)
800 SCREEN 0, 0, 1
810 END
