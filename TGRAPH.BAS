10 WIDTH 80:SCREEN 0:KEY OFF:CLS:ON ERROR GOTO 120:GOSUB 130
20 IF RES=80 THEN SCREEN 2 ELSE SCREEN 1:COLOR 1,1
30 CLS:GOSUB 370:IF RES=40 THEN OUT &H3D8,202
40 LINE (4*RES,0)-(4*RES,199),2
50 LINE (7/8*RES,100)-(57/8*RES,100),2
60 FOR X=-25/8*RES TO 25/8*RES STEP .2
80 PSET (X+4*RES,-FNF(X)+100)
90 A$=INKEY$:IF A$=CHR$(27) THEN END
100 NEXT X
110 END
120 RESUME NEXT
130 PRINT "Trigonometric [G]raph or [W]ave?":GW$=INPUT$(1)
140 IF GW$="g" OR GW$="G" THEN 800
150 IF GW$="w" OR GW$="W" THEN 500
160 LOCATE CSRLIN-1,1
170 PRINT STRING$(79,32);
180 BEEP
190 LOCATE CSRLIN-1,1
200 GOTO 130
210 PRINT "Choose one trigonometric function graph :-"
220 PRINT "  1. ATN (Y/X) [arctan y/x ]"
230 PRINT "  2. COS (é) [cos é]"
240 PRINT "  3. SIN (é) [sin é]"
250 PRINT "  4. TAN (é) [tan é]"
260 PRINT "  5. cosec é function [not supported by GW-BASIC]"
270 PRINT "  6. sec é function [not supported by GW-BASIC]"
280 PRINT "  7. cot é function [not supported by GW-BASIC]"
290 PRINT "  8. arcsin y/h function [not supported by GW-BASIC]"
300 PRINT "  9. arccos x/h function [not supported by GW-BASIC]"
310 PRINT " 10. arcsec h/x function [not supported by GW-BASIC]"
320 PRINT " 11. arccosec h/y function [not supported by GW-BASIC]"
330 PRINT " 12. arccot x/y function [not supported by GW-BASIC]"
340 PRINT
350 INPUT "Graph Number: ",EQNO
360 RETURN
370 ON EQNO GOTO 380,390,400,410,420,430,440,450,460,470,480,490
380 LOCATE 1,1:PRINT "ATN graph":DEF FNF(X)=-50*ATN(X/RES):RETURN
390 LOCATE 1,1:PRINT "COS graph":DEF FNF(X)=-50*COS(X/RES):RETURN
400 LOCATE 1,1:PRINT "SIN graph":DEF FNF(X)=-50*SIN(X/RES):RETURN
410 LOCATE 1,1:PRINT "TAN graph":DEF FNF(X)=-50*TAN(X/RES):RETURN
420 LOCATE 1,1:PRINT "cosec graph":DEF FNF(X)=-50*1/(SIN(X/RES)):RETURN
430 LOCATE 1,1:PRINT "sec graph":DEF FNF(X)=-50*1/(COS(X/RES)):RETURN
440 LOCATE 1,1:PRINT "cot graph":DEF FNF(X)=-50*1/(TAN(X/RES)):RETURN
450 LOCATE 1,1:PRINT "arcsin graph":DEF FNF(X)=-50*(2*ATN((X/RES)/(1+SQR(1-(X/RES)*(X/RES))))):RETURN
460 LOCATE 1,1:PRINT "arccos graph":DEF FNF(X)=-50*(3.141592653858#/2-2*ATN((X/RES)/(1+SQR(1-(X/RES)*(X/RES))))):RETURN
470 LOCATE 1,1:PRINT "arcsec graph":DEF FNF(X)=-50*(SQR(((X/RES)^2)-1)):RETURN
480 LOCATE 1,1:PRINT "arccosec graph":DEF FNF(X)=-50*(1/SQR(((X/RES)^2)-1)):RETURN
490 LOCATE 1,1:PRINT "arccot graph":DEF FNF(X)=-50*ATN(1/(X/RES)):RETURN
500 GOSUB 710
510 ON ERROR GOTO 700
520 KEY OFF:SCREEN 1:COLOR 1,1:CLS
530 LOCATE 1,1
540 IF CST=1 THEN PRINT "cos wave"
550 IF CST=2 THEN PRINT "sin wave"
560 IF CST=3 THEN PRINT "tan wave"
570 DEF FN PI#=3.141592653858#
580 LINE (0,100)-(320,100),1
590 FOR A=0 TO 11
600 LINE (A*FN PI#*10,100-8)-(A*FN PI#*10,100+8),2
610 LOCATE 15,1:PRINT "0";:LOCATE 15,7:PRINT "180";
620 LOCATE 15,15:PRINT "360";:LOCATE 15,23:PRINT"540";
630 LOCATE 15,31:PRINT "720";
640 NEXT
650 FOR X=0 TO FN PI#*5 STEP .01
660 GOSUB 760
670 PSET (X*20,Y+100)
680 NEXT X
690 A$=INPUT$(1):END
700 RESUME 680
710 INPUT "COS Wave/SIN Wave/TAN Wave (C/S/T): ",CST$
720 IF CHR$(ASC(CST$) AND 223)="C" THEN CST=1
730 IF CHR$(ASC(CST$) AND 223)="S" THEN CST=2
740 IF CHR$(ASC(CST$) AND 223)="T" THEN CST=3
750 RETURN
760 ON CST GOTO 770,780,790
770 Y=-COS(X)*40:RETURN
780 Y=-SIN(X)*40:RETURN
790 Y=-TAN(X)*40:RETURN
800 PRINT "High or low resolution? (H/L): ";
810 GW$=INPUT$(1)
820 IF CHR$(ASC(GW$) OR 32)="h" THEN RES=80 ELSE RES=40
830 GOTO 210
