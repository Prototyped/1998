5 ON ERROR GOTO 20000
10 KEY OFF: FOR A = 1 TO 10: KEY A, "": KEY 2, "RUN 55" + CHR$(13): NEXT
20 KEY ON: SCREEN 0: COLOR 7, 1, 5: CLS
30 'PRINT "Enter wave equation and press Enter and F2 to resume." + CHR$(13) + "60 DEF FN W (X, T) = <wave expression in variables x and t here>" + CHR$(13)
40 'EDIT 60
50 'END
55 DEF FNPI = 3.141592653589793#
60 DEF FNW (X, T) = SIN(2 * FNPI * T - 2 * FNPI * X) - COS(2 * FNPI * T + 2 * FNPI * X)
70 INPUT "Enter lower x bound: ", X.LOWER
80 INPUT "Enter upper x bound: ", X.UPPER
90 INPUT "Enter lower y bound: ", Y.LOWER
100 PRINT "Square graph? (Y/N) "; : SQUARE.GRAPH$ = INPUT$(1): PRINT
110 IF CHR$(ASC(SQUARE.GRAPH$) AND &HDF) <> "Y" THEN INPUT "Enter upper y bound: ", Y.UPPER ELSE Y.UPPER = X.UPPER - X.LOWER + Y.LOWER: X.LOWER = X.LOWER * 4 / 3: X.UPPER = X.UPPER * 4 / 3
1000 KEY OFF: SCREEN 1, 0: COLOR 1: CLS
1010 X.SCALE = INT(LOG(X.UPPER - X.LOWER) / LOG(10))
1020 Y.SCALE = INT(LOG(Y.UPPER - Y.LOWER) / LOG(10))
1030 IF INT((X.UPPER - X.LOWER) / 10 ^ X.SCALE) < 3 THEN X.SCALE = X.SCALE - 1
1040 IF INT((Y.UPPER - Y.LOWER) / 10 ^ Y.SCALE) < 3 THEN Y.SCALE = Y.SCALE - 1
1050 LOCATE 1, 1: PRINT X.SCALE;
1060 LOCATE 1, 36: PRINT Y.SCALE;
1070 X.SCALE = 10 ^ X.SCALE
1080 Y.SCALE = 10 ^ Y.SCALE
1090 WINDOW (X.LOWER, Y.LOWER)-(X.UPPER, Y.UPPER)
1100 TIME.LAP = TIMER: TIME.OLD = 0
1110 WHILE INKEY$ = ""
1120 LINE (X.LOWER, 0)-(X.UPPER, 0), 2
1130 LINE (0, Y.LOWER)-(0, Y.UPPER), 2
1140 FOR C = INT(X.LOWER / X.SCALE) TO INT(X.UPPER / X.SCALE)
1150 LINE (C * X.SCALE, -.015 * (Y.UPPER - Y.LOWER))-(C * X.SCALE, .015 * (Y.UPPER - Y.LOWER)), 2
1160 NEXT
1170 FOR C = INT(Y.LOWER / Y.SCALE) TO INT(Y.UPPER / Y.SCALE)
1180 LINE (-9 / 1280 * (X.UPPER - X.LOWER), C * Y.SCALE)-(9 / 1280 * (X.UPPER - X.LOWER), C * Y.SCALE), 2
1190 NEXT
1200 FOR X = X.LOWER TO X.UPPER STEP (X.UPPER - X.LOWER) / 100
1210 'PSET (X, FNW(X, TIME.OLD)), 0
1220 PSET (X, FNW(X, TIME.LAP)), 3
1230 NEXT
1240 TIME.OLD = TIME.LAP: TIME.LAP = TIMER
1250 CLS : WEND
10000 KEY 1, "LIST "
10010 KEY 2, "RUN" + CHR$(13)
10020 KEY 3, "LOAD" + CHR$(34)
10030 KEY 4, "SAVE" + CHR$(34)
10040 KEY 5, "CONT" + CHR$(13)
10050 KEY 6, "," + CHR$(34) + "LPT1" + CHR$(34) + CHR$(13)
10060 KEY 7, "TRON" + CHR$(13)
10070 KEY 8, "TROFF" + CHR$(13)
10080 KEY 9, "KEY "
10090 KEY 10, "SCREEN 0,0,0" + CHR$(13)
10100 KEY ON
10200 SCREEN 0: COLOR 7, 1, 5: WIDTH 80: CLS
19999 END
20000 IF (ERR = 6 OR ERR = 11) AND ERL = 65535 THEN RESUME NEXT
20010 SCREEN 0: WIDTH 80: COLOR 7, 1, 5: CLS : PRINT "Error "; ERR; " encountered executing line "; ERL; "."
65529 END

